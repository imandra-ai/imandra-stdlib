(** {1 List} *)

type 'a t = 'a List.t


theorem thm_append_assoc x y z = x @ y @ z = (x @ y) @ z
  [@@by auto] [@@rw]

theorem thm_append_id x = x @ [] = x [@@by auto] [@@rw]

theorem thm_append_len a b = List.length (a @ b) = List.length a + List.length b
  [@@by auto] [@@rw]

theorem thm_append_elim x y a b =
  List.append x [ a ] = List.append y [ b ] = (x = y && a = b)
  [@@by induct ~on_vars:["x"; "y"] ()] [@@rw]

theorem thm_rev_elim x y = List.rev x = List.rev y = (x = y)
  [@@by induct ~on_vars:["x"; "y"] ()] [@@rw]

theorem thm_rev_append x y = List.rev (x @ y) = List.rev y @ List.rev x
  [@@by auto] [@@rw]

theorem thm_rev_rev_id x = List.rev (List.rev x) = x
  [@@by auto] [@@rw]

theorem thm_rev_empty x = List.rev x = [] = (x = [])
  [@@by auto] [@@rw]

theorem thm_rev_len x = List.length (List.rev x) = List.length x
  [@@by auto] [@@rw]

theorem thm_range_len x y = x < y ==> List.length List.(x -- y) = y - x
  [@@by auto] [@@rw]

theorem thm_forall_map f g l =
  (List.for_all f (List.map g l) [@trigger]) = List.for_all (fun x -> f (g x)) l
  [@@by auto] [@@rw]

theorem thm_forall_filter f g l =
  (List.for_all f (List.filter g l) [@trigger])
  = List.for_all (fun x -> g x ==> f x) l
  [@@by auto] [@@rw]

theorem thm_exists_map f g l =
  (List.exists f (List.map g l) [@trigger]) = List.exists (fun x -> f (g x)) l
  [@@by auto] [@@rw]

theorem thm_exists_filter f g l =
  (List.exists f (List.filter g l) [@trigger])
  = List.exists (fun x -> f x && g x) l
  [@@by auto] [@@rw]

theorem thm_forall_exists u e xs =
  (List.for_all u xs [@trigger 0i])
  && (List.exists e xs [@trigger 0i])
     ==>
     match List.find e xs with
     | Some el -> u el && e el
     | None -> false
  [@@by auto] [@@fc]

let sort ~leq l =
  List.fold_left (fun acc x -> List.insert_sorted ~leq x acc) [] l

theorem thm_len_append x y =
  List.length (List.append x y) = List.length x + List.length y
  [@@by auto] [@@rw]

let rec remove_opt_aux f acc (xs : 'order list) =
  match xs with
  | [] -> None
  | o :: xs when f o -> Some (o, List.rev acc @ xs)
  | o :: xs -> remove_opt_aux f (o :: acc) xs

theorem thm_list_remove_opt_aux_len f acc xs =
  match remove_opt_aux f acc xs with
  | None -> true
  | Some (_, ys) -> List.length ys < List.length acc + List.length xs
  [@@by auto] [@@fc]

let remove_opt f xs = remove_opt_aux f [] xs

theorem thm_list_remove_opt_len f xs =
  match remove_opt f xs with
  | None -> true
  | Some (_, ys) -> List.length ys < List.length xs
  [@@by using (quote_term (thm_list_remove_opt_aux_len f [] xs)) @> auto]
  [@@fc]

let remove_all p l = List.filter (fun x -> not (p x)) l

theorem thm_list_remove_not_exists_bridge p l l' =
  l = remove_all p l' ==> not (List.exists p l)
  [@@fc] [@@by auto]

let rec update_first_aux pred upd acc (xs : 'order list) =
  match xs with
  | [] -> []
  | o :: xs when pred o -> List.rev acc @ (upd o :: xs)
  | o :: xs -> update_first_aux pred upd (o :: acc) xs

let update_first pred upd xs = update_first_aux pred upd [] xs

let rec partition_rev_map_aux l_rev r_rev f xs =
  match xs with
  | x :: xs ->
    let l_rev, r_rev =
      match f x with
      | Left l -> l :: l_rev, r_rev
      | Right r -> l_rev, r :: r_rev
    in
    partition_rev_map_aux l_rev r_rev f xs
  | [] -> l_rev, r_rev

theorem thm_len_rev xs = List.length (List.rev xs) = List.length xs
  [@@by auto] [@@rw]

theorem thm_len_partition_rev_map_aux l_rev r_rev f xs =
  let ys, zs = (partition_rev_map_aux l_rev r_rev f xs [@trigger]) in
  List.length ys + List.length zs
  = List.length l_rev + List.length r_rev + List.length xs
  [@@by auto] [@@fc]

let partition_rev_map f xs = partition_rev_map_aux [] [] f xs

theorem thm_len_partition_rev_map f xs =
  let ys, zs = partition_rev_map f xs in
  List.length ys + List.length zs = List.length xs
  [@@by using (quote_term (thm_len_partition_rev_map_aux [] [] f xs)) @> auto]
  [@@fc]

let partition_map f xs =
  let a, b = partition_rev_map_aux [] [] f xs in
  List.rev a, List.rev b

theorem thm_len_partition_map f xs =
  let ys, zs = partition_map f xs in
  List.length ys + List.length zs = List.length xs
  [@@by using (quote_term (thm_len_partition_rev_map_aux [] [] f xs)) @> auto]
  [@@fc]

let partition f xs =
  partition_map
    (fun x ->
      if f x then
        Left x
      else
        Right x)
    xs

theorem thm_len_partition f xs =
  let ys, zs = partition f xs in
  List.length ys + List.length zs = List.length xs
  [@@by auto] [@@disable partition_map] [@@fc]

theorem thm_len_insert_sorted ~leq x l =
  List.length (List.insert_sorted ~leq x l) = List.length l + 1
  [@@by auto] [@@rw]

theorem thm_len_sort ~leq l = List.length (sort ~leq l) = List.length l
  [@@by auto] [@@rw]

let rec group_by_aux ~key_fn ~merge_vals_fn al l =
  match l with
  | [] -> al
  | (k, v) :: l ->
    let k = key_fn k in
    let al =
      match remove_opt (fun (k', _) -> k = k') al with
      | Some ((_, v'), al) -> (k, merge_vals_fn v' v) :: al
      | None -> (k, v) :: al
    in
    group_by_aux ~key_fn ~merge_vals_fn al l
  [@@adm l]

let group_by ~key_fn ~merge_vals_fn l = group_by_aux ~key_fn ~merge_vals_fn [] l

let uniq ~eq l =
  let rec uniq eq acc l =
    match l with
    | [] -> List.rev acc
    | x :: xs when List.exists (eq x) xs -> uniq eq acc xs
    | x :: xs -> uniq eq (x :: acc) xs
  in
  uniq eq [] l

let rec find_map f = function
  | [] -> None
  | x :: xs ->
    (match f x with
    | Some v -> Some v
    | None -> find_map f xs)

let cons_when b x l =
  if b then
    x :: l
  else
    l

let cons_maybe x l =
  match x with
  | None -> l
  | Some x -> x :: l

let rec for_all2 f l1 l2 =
  let open Result in
  match l1, l2 with
  | [], [] -> Ok true
  | x :: l1, y :: l2 -> for_all2 f l1 l2 >|= ( && ) (f x y)
  | _, _ -> Error "for_all2: Lengths of l1 and l2 don't match"

let rec for_all2_t f l1 l2 =
  match l1, l2 with
  | [], [] -> true
  | x :: l1, y :: l2 -> f x y && for_all2_t f l1 l2
  | _, _ -> false

let rec map2_t f l1 l2 =
  match l1, l2 with
  | [], _ -> []
  | _, [] -> []
  | x :: l1, y :: l2 -> f x y :: map2_t f l1 l2
